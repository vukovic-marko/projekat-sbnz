package sbnz.integracija

import sbnz.integracija.example.facts.Zapisnik;
import sbnz.integracija.example.facts.KaznaPrekoracenjaBrzine;
import sbnz.integracija.example.facts.Kazna;
import sbnz.integracija.example.facts.Vozac;

import java.util.List
import java.util.ArrayList

global List lista;
global List pravilo2;
global List pravilo3;

rule "prekoracenje brzine za vozaca sa trajnom dozvolom"
agenda-group "modul1"
	when
		$z: Zapisnik( $vozac: vozac, ostvarenaBrzina > dozvoljenaBrzina, prekoracenjeBrzine == null )
		$v: Vozac( this == $vozac, tipDozvole == Vozac.TipDozvole.TRAJNA )
	then
		KaznaPrekoracenjaBrzine b = new KaznaPrekoracenjaBrzine();
		Double prekoracenje = $z.getOstvarenaBrzina() - $z.getDozvoljenaBrzina();
		b.setIznosPrekoracenja(prekoracenje);
        b.setObradjena(false);
        b.setClan(null);

        insert(b);
		modify($z) { setPrekoracenjeBrzine(b) }
end

rule "prekoracenje brzine za vozaca sa probnom dozvolom na autoputu"
agenda-group "modul1"
	when
		$z: Zapisnik( $vozac: vozac, dozvoljenaBrzina == 130.0, ostvarenaBrzina > 100.0, prekoracenjeBrzine == null )
		$v: Vozac( this == $vozac, tipDozvole == Vozac.TipDozvole.PROBNA )
	then
		KaznaPrekoracenjaBrzine b = new KaznaPrekoracenjaBrzine();
		Double prekoracenje = $z.getOstvarenaBrzina() - 100.0;
		b.setIznosPrekoracenja(prekoracenje);
        b.setObradjena(false);
        b.setClan(null);

        insert(b);
		modify($z) { setPrekoracenjeBrzine(b) }
end

rule "prekoracenje brzine za vozaca sa probnom dozvolom na motoputu"
agenda-group "modul1"
	when
		$z: Zapisnik( $vozac: vozac, dozvoljenaBrzina == 100.0, ostvarenaBrzina > 90.0, prekoracenjeBrzine == null )
		$v: Vozac( this == $vozac, tipDozvole == Vozac.TipDozvole.PROBNA )
	then
		KaznaPrekoracenjaBrzine b = new KaznaPrekoracenjaBrzine();
		Double prekoracenje = $z.getOstvarenaBrzina() - 90.0;
		b.setIznosPrekoracenja(prekoracenje);
        b.setObradjena(false);
        b.setClan(null);

        insert(b);
		modify($z) { setPrekoracenjeBrzine(b) }
end

rule "prekoracenje brzine za vozaca sa probnom dozvolom na ostalim putevima"
agenda-group "modul1"
salience -1
	when 
		$z: Zapisnik( $vozac: vozac, prekoracenjeBrzine == null, dozvoljenaBrzina != null, ostvarenaBrzina!= null, ostvarenaBrzina > 0.9 * dozvoljenaBrzina )
		$v: Vozac( this == $vozac, tipDozvole == Vozac.TipDozvole.PROBNA )
	then
		KaznaPrekoracenjaBrzine b = new KaznaPrekoracenjaBrzine();
		Double prekoracenje = $z.getOstvarenaBrzina() - 0.9 * $z.getDozvoljenaBrzina();
		b.setIznosPrekoracenja(prekoracenje);
        b.setObradjena(false);
        b.setClan(null);

        insert(b);
		modify($z) { setPrekoracenjeBrzine(b) }
end